// RUN: cir-opt %s -cir-to-mlir -o %t.mlir
// RUN: FileCheck %s --input-file %t.mlir


!s32i = !cir.int<s, 32>
!u32i = !cir.int<u, 32>
module {
  cir.func @foo(%arg0: !cir.ptr<!u32i>, %arg1: !cir.ptr<!u32i>, %arg2: !s32i) {
    %0 = cir.alloca !cir.ptr<!u32i>, !cir.ptr<!cir.ptr<!u32i>>, ["a", init] {alignment = 8 : i64}
    %1 = cir.alloca !cir.ptr<!u32i>, !cir.ptr<!cir.ptr<!u32i>>, ["b", init] {alignment = 8 : i64}
    %2 = cir.alloca !s32i, !cir.ptr<!s32i>, ["gid", init] {alignment = 4 : i64}
    cir.store %arg0, %0 : !cir.ptr<!u32i>, !cir.ptr<!cir.ptr<!u32i>>
    cir.store %arg1, %1 : !cir.ptr<!u32i>, !cir.ptr<!cir.ptr<!u32i>>
    cir.store %arg2, %2 : !s32i, !cir.ptr<!s32i>
    %3 = cir.load %0 : !cir.ptr<!cir.ptr<!u32i>>, !cir.ptr<!u32i>
    %4 = cir.load %2 : !cir.ptr<!s32i>, !s32i
    %5 = cir.ptr_stride(%3 : !cir.ptr<!u32i>, %4 : !s32i), !cir.ptr<!u32i>
    %6 = cir.load %5 : !cir.ptr<!u32i>, !u32i
    %7 = cir.load %1 : !cir.ptr<!cir.ptr<!u32i>>, !cir.ptr<!u32i>
    %8 = cir.load %2 : !cir.ptr<!s32i>, !s32i
    %9 = cir.ptr_stride(%7 : !cir.ptr<!u32i>, %8 : !s32i), !cir.ptr<!u32i>
    cir.store %6, %9 : !u32i, !cir.ptr<!u32i>
    cir.return
  }
}


// CHECK: module {
// CHECK:   func.func @foo(%arg0: memref<?xi32>, %arg1: memref<?xi32>, %arg2: i32) {
// CHECK:     %alloca = memref.alloca() {alignment = 8 : i64} : memref<memref<?xi32>>
// CHECK:     %alloca_0 = memref.alloca() {alignment = 8 : i64} : memref<memref<?xi32>>
// CHECK:     %alloca_1 = memref.alloca() {alignment = 4 : i64} : memref<i32>
// CHECK:     memref.store %arg0, %alloca[] : memref<memref<?xi32>>
// CHECK:     memref.store %arg1, %alloca_0[] : memref<memref<?xi32>>
// CHECK:     memref.store %arg2, %alloca_1[] : memref<i32>
// CHECK:     %0 = memref.load %alloca[] : memref<memref<?xi32>>
// CHECK:     %1 = memref.load %alloca_1[] : memref<i32>
// CHECK:     %2 = arith.index_cast %1 : i32 to index
// CHECK:     %3 = memref.load %0[%2] : memref<?xi32>
// CHECK:     %4 = memref.load %alloca_0[] : memref<memref<?xi32>>
// CHECK:     %5 = memref.load %alloca_1[] : memref<i32>
// CHECK:     %6 = arith.index_cast %5 : i32 to index
// CHECK:     memref.store %3, %4[%6] : memref<?xi32>
// CHECK:     return
// CHECK:   }
// CHECK: }

